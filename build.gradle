/*
 * This file is part of the DITA Open Toolkit project.
 *
 * Copyright 2015 Jarno Elovirta
 *
 * See the accompanying LICENSE file for applicable license.
 */
apply plugin: 'java-library'

group = 'org.dita.pdf2'
version = '3.6.1'
description = """DITA Open Toolkit PDF plug-in for RenderX."""

repositories {
    mavenCentral()
}
dependencies {
    implementation group: 'org.dita-ot', name: 'dost', version: '3.6.0'
}
sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
}

jar.archiveFileName = "${project.name}.jar"
jar.include "com/idiominc/**"

task dist(type: Zip, dependsOn: [jar]) {
    from(configurations.archives.artifacts.files) {
        into("lib")
    }
    from(".") {
        include "LICENSE"
        include "README.md"
    }
    from(".") {
        include "build.xml"
        include "cfg/**"
        include "xsl/**"
    }
    from(".") {
        include "plugin.xml"
        expand(
                id: "${project.group}.${project.archivesBaseName}",
                version: project.version,
                jar: "lib/${jar.archiveFileName.get()}"
        )
    }
    archiveFileName = "${project.group}.${project.archivesBaseName}-${project.version}.zip"
}
